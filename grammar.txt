Program     ->  (DecVar | DecFunc)*

DecVar      ->  Type ID ('=' E1)? ';'

DecFunc     ->  DEF Type ID '(' ParamList? ')' Block

ParamList   ->  Type ID (',' Type ID)*

Block       ->  '{' DecVar* Stmt* '}'

Stmt        ->  Assign ';' |
                FuncCall ';' |
                IF '(' E1 ')' Block (ELSE Block)? |
                WHILE '(' E1 ')' Block |
                RETURN E1? ';' |
                BREAK ';' |
                CONTINUE ';'

Assign      ->  ID '=' E1

FuncCall    ->  ID '(' ArgList? ')'

ArgList     ->  E1 (',' E1)*

E1          ->  E1 '||' E2 |
                E2
                
E2          ->  E2 '&&' E3 |
                E3

E3          ->  E3 '==' E4 |
                E4

E4          ->  E4 '<' E5 |
                E4 '<=' E5 |
                E4 '>' E5 |
                E4 '>=' E5 |
                E5

E5          ->  E5 '+' E6 |
                E5 '-' E6 |
                E6

E6          ->  E6 '*' E7 |
                E6 '/' E7 |
                E7

E7          ->  '!' E7 |
                E8

E8          ->  '-' E8 |
                E9

E9          ->  DEC |
                ID |
                FuncCall |
                '(' E1 ')'

Type        ->  INT | VOID
