Program     ->  Instr

Instr       ->  DecVar Instr |
                DecFunc Instr |
                ε

DecVar      ->  Type ID Assign? ';'

Assign?     ->  Assign |
                ε

DecFunc     ->  DEF Type ID '(' ParamList? ')' Block

ParamList?  ->  ParamList |
                ε

ParamList   ->  Type ID Param*

Param*      ->  ',' Type ID Param* |
                ε

Block       ->  '{' DecVar* Stmt* '}'

DecVar*     ->  DecVar DecVar* |
                ε

Stmt*       ->  Stmt Stmt* |
                ε

Stmt        ->  ID Decl ';' |
                IF '(' E1 ')' Block FI? |
                WHILE '(' E1 ')' Block |
                RETURN E1? ';' |
                BREAK ';' |
                CONTINUE ';'

FI?         ->  ELSE Block |
                ε

E1?         ->  E1 |
                ε

Decl        ->  Assign |
                FuncCall |

Assign      ->  '=' E1

FuncCall    ->  '(' ArgList? ')'

ArgList?    ->  ArgList |
                ε

ArgList     ->  E1 Arg*

Arg*        ->  ',' E1 Arg* |
                ε

E1          ->  E2 E1'

E1'         ->  '||' E2 E1' |
                ε

E2          ->  E3 E2'

E2'         ->  '&&' E3 E2' |
                ε

E3          ->  E4 E3'

E3'         ->  '==' E4 E3' |
                '!=' E4 E3' |
                ε

E4          ->  E5 E4'

E4'         ->  '<' E5 E4' |
                '<=' E5 E4' |
                '>' E5 E4' |
                '>=' E5 E4' |
                ε

E5          ->  E6 E5'

E5'         ->  '+' E6 E5' |
                '-' E6 E5' |
                ε

E6          ->  E7 E6'

E6'         ->  '*' E7 E6' |
                '/' E7 E6' |
                ε

E7          ->  '!' E7 |
                E8

E8          ->  '-' E8 |
                E9

E9          ->  DEC |
                ID E10 |
                '(' E1 ')'

E10         ->  FuncCall |
                ε

Type        ->  INT | VOID
